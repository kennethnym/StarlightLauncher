apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlinx-serialization"
apply plugin: "dagger.hilt.android.plugin"

def apiKeyFile = rootProject.file("apikey.properties")
def apiKeys = new Properties()
apiKeys.load(new FileInputStream(apiKeyFile))

android {
    compileSdk 33

    defaultConfig {
        applicationId "kenneth.app.starlightlauncher"
        minSdk 23
        targetSdk 33
        versionCode 10026
        versionName "1.0.0-beta.6"

        renderscriptTargetApi 30
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // api keys from apikey.properties
        buildConfigField("String", "OPEN_WEATHER_API_KEY", apiKeys["OPEN_WEATHER_API_KEY"])
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Codemagic
                storeFile file(System.getenv()["CM_KEYSTORE_PATH"])
                storePassword System.getenv()["CM_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["CM_KEY_ALIAS"]
                keyPassword System.getenv()["CM_KEY_PASSWORD"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(path: ':starlight-launcher-api')
    implementation project(path: ':app-search-module')
    implementation project(path: ':file-search-module')
    implementation project(path: ':notes-widget')
    implementation project(path: ':unit-converter-widget')
    implementation project(path: ':math-search-module')
    implementation project(path: ':wifi-control-module')
    implementation project(path: ':app-shortcut-search-module')
    implementation project(path: ':url-opener')
    implementation project(path: ':contact-search-module')
    implementation project(path: ':web-search-module')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
    implementation "com.google.android.material:material:1.6.1"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.activity:activity-ktx:1.6.0-alpha05"
    implementation "androidx.fragment:fragment:1.5.0"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "com.github.Keelar:ExprK:master"
    implementation "com.squareup.okhttp3:okhttp:4.9.3"
    implementation "com.beust:klaxon:5.0.1"
    implementation "com.github.bumptech.glide:glide:4.11.0"
    implementation 'com.github.takusemba:spotlight:2.0.5'
    implementation "dev.chrisbanes.insetter:insetter:0.6.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}